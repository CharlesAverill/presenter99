1 REM  === SLIDE DATA === 
10 DATA "TITLE","My Presentation","Charles Averill",16,2
20 DATA "SLIDE","Slide 1"
30 DATA "TEXT","Here is some text"
40 DATA "LIST","First","Second","Third","END"
50 DATA "COLORS",5,16
60 DATA "SLIDE","Slide 2"
80 DATA "TEXT","Thanks for watching my"
90 DATA "TEXT","presentation!"
95 DATA "PAUSE"
100 DATA "SPACING",-1
110 DATA "TEXT","Made with Presenter99"
1900 REM = DEFINES =
1910 DEF CENTERCOL(S$)=((32-LEN(S$))/2)-1
2000 DATA "END_SLIDESHOW"
2001 REM  === SLIDESHOW ENGINE ===
2010 REM  = READ TITLE INFO =
2020 READ TOK$
2030 IF TOK$<>"TITLE" THEN 2040 ELSE 2060
2040 ERR$="EXPECTED TOKEN 'TITLE' BUT GOT '"&TOK$&"'"
2050 GOTO 2000
2060 READ TITLE$,SUBTITLE$,FG,BG
2070 REM  = DISPAY TITLE SCREEN = 
2080 GOSUB 3170
2090 PRINT TAB(CENTERCOL(TITLE$));TITLE$:""
2100 PRINT TAB(CENTERCOL(SUBTITLE$));SUBTITLE$
2110 ROW=15
2170 REM = RENDER SLIDES = 
2180 READ TOK$
2190 IF TOK$="SLIDE" THEN 2500
2200 IF TOK$="TEXT" THEN 2700
2210 IF TOK$="COLORS" THEN 2600
2220 IF TOK$="LIST" THEN 2800
2230 IF TOK$="SPACING" THEN 2650
2240 IF TOK$="END_SLIDESHOW" THEN 2900
2250 IF TOK$="PAUSE" THEN 2950
2260 ERR$="UNKNOWN SLIDE TOKEN: '"&TOK$&"'"
2270 GOTO 3000
2500 REM == RENDER SLIDE TITLE ==
2510 GOSUB 3100
2520 GOSUB 3060
2530 IF COLORSWAP=0 THEN 2540
2535 GOSUB 3170
2540 READ TITLE$
2550 CALL CLEAR
2560 PRINT TAB(CENTERCOL(TITLE$));TITLE$
2570 PRINT ""
2580 ROW=3
2590 GOTO 2180
2600 REM == CHANGE COLORS ==
2610 READ FG,BG
2620 COLORSWAP=1
2630 GOTO 2180
2650 REM == SPACING ==
2655 READ N
2660 IF N<>-1 THEN 2670
2665 N=22-ROW
2670 FOR I=1 TO N
2675 PRINT
2680 ROW = ROW+1
2685 NEXT I
2690 GOTO 2180
2700 REM = RENDER SLIDE TEXT =
2710 READ TEXT$
2720 PRINT TEXT$:""
2730 ROW=ROW+2+INT(LEN(TEXT$)/32)
2740 GOTO 2180
2800 REM = RENDER LISTS =
2810 READ ITEM$
2820 IF ITEM$="END" THEN 2180
2830 PRINT "- ";ITEM$
2840 ROW=ROW+1
2850 GOTO 2810
2900 REM = END SLIDESHOW =
2910 GOSUB 3100
2920 GOSUB 3060
2930 GOTO 3030
2950 REM = PAUSE =
2960 GOSUB 3060
2970 GOTO 2180
3000 REM = ERROR SUBROUTINE =
3010 PRINT "ERROR: ",ERR$:" "
3020 STOP
3030 REM = SAFE EXIT SUBROUTINE =
3040 CALL CLEAR
3050 STOP
3060 REM = WAIT FOR KEYPRESS =
3070 CALL KEY(0,K,S)
3080 IF S=0 THEN 3070
3090 RETURN
3100 REM = FILL TO BOTTOM IF NEEDED =
3110 IF ROW>=24 THEN 3160
3120 PRINT ""
3140 ROW=ROW+1
3150 GOTO 3110
3160 RETURN
3170 REM = SET COLORS =
3180 CALL CLEAR
3190 CALL SCREEN(BG)
3200 FOR I=1 TO 16
3210 CALL COLOR(I,FG,BG)
3220 NEXT I
3230 COLORSWAP = 0
3240 RETURN
